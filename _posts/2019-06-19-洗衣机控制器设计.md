---
layout:     post
title:      洗衣机控制器设计
subtitle:   
date:       2019-06-19
author:     yzmb2411
header-img: 
catalog: 	 true
tags:
    - FPGA
---
##  1、设计任务与指标

设计一个洗衣机控制器，使洗衣机作如下运转。

 - 1）定时启动→正转20s→暂停10s→定时不到，重复上述过程。
 - 2）若定时到，则停止，并发出音响信号。
 - 3）用二个数码管显示洗涤的预置时间45min，按倒计时方式对洗涤过程作计时显示，直到时间到停机；洗涤过程由开始信号开始。
 - 4）三只LED灯分别表示正转、反转、暂停三个状态。
##  1、设计原理与方案

### 1、工作原理

洗衣机控制器的设计主要是定时器的设计，由一片FPGA和外围电路构成了电路控制部分。FPGA接收键盘的控制命令，控制洗衣机的进水、排水、水位和洗衣机的工作状态，并控制显示工作状态以及设定直流电机速度、正反转控制、制动控制、起停控制和运动状态控制。对FPGA芯片的编程采用verilog进行设计，设计分为三层实现，顶层实现整个芯片的功能。顶层和中间层多采用由verilog的元件例化语句实现，中间层由无刷直流电机控制、运行模式选择、洗涤模式选择、定时器、显示控制、键盘扫描、水位控制以及对滞留电机控制板进行速度设定、正反转控制、启停控制等模块组成，他们分别调用底层模块。

### 2、设计方案

洗衣机控制器电路主要由五大部分组成，包括分频器、计数器、状态控制及预置时间模块、时间显示模块、扫描显示驱动模块。

##  3、设计与实现

### 1、	系统模块设计

#### 1）分频器

本分频器的任务是提供1S的时钟源，并提供1khz的扫描时钟。FPGA开发板提供的系统时钟是50MHZ，因此分频器对50MHZ的时钟源进行50M分频，从而产生1hz信号，对50MHZ的时钟源进行100K分频从而产生1KHZ信号。分频器程序如下：

```verilog
module clkdiv(  
                  input wire clk,
                  output reg clk1hz,
                  output reg clk1k
                                        );
reg [25:0] cnt;
reg [15:0] cnt1k;
always @ (posedge clk)
  begin
    if(cnt == 26'd24_999_999)
       begin
         cnt <= 0;
         clk1hz <= !clk1hz;
        end
     else 
         cnt <= cnt + 26'd1;
   end 
always @ (posedge clk)
     begin
       if(cnt1k == 16'd24_999)
          begin
            cnt1k <= 0;
            clk1k <= !clk1k;
           end
        else 
            cnt1k <= cnt1k + 16'd1;
      end                                                  
endmodule
```

#### 2）60S计数器

该模块是对1hz时钟信号进行0~59的循环计数，目的是为状态控制模块提供运行时间。输入信号有1hz计数基准时钟clk1hz，开始信号为start。根据题目要求，当开始信号有效时，计数器输出为0，采用给计数累加信号置0的方式，如if(start)  cnt <=0 .注：开始信号为低电平有效，其源程序如下：

```verilog 
module counter(
                   input wire clk1hz,
                   input wire start,
                   output wire [5:0] count
                                             );
reg [5:0] cnt;
always @ (posedge clk1hz or negedge start)
  begin
     if(!start)
         cnt <= 0;
     else if(cnt == 6'd59)
         cnt <= 0;
     else 
         cnt <= cnt + 6'd1;
   end
  assign count = cnt;           
endmodule
```
#### 3）状态控制器

状态控制器是洗衣机控制系统的核心，其任务是控制洗衣状态（正转、反转和暂停），同时输出洗衣剩余时间。

分频计时模块counter产生的60S时间为4个状态时间段：0~19s、20~29s、30~49s、50~59s。当计数器小于20s时，系统正处于正传状态；当计数器大于等于20s小于30s时，系统处于暂停状态；当计数器大于等于30s小于50s时，系统正处于反传状态；当计数器大于等于50s小于60s时，系统处于暂停状态。

状态控制模块除了正常工作时序外，还要控制剩余时间的输出值。当循环完1min（4个状态）时，若剩余预置时间不为零，则显示时间减1min。

Clk为50MHZ的系统时钟，clk1hz为频率为1hz的时钟信号，用于状态控制器的同步工作；start为开始信号，低电平0有效；count(5:0)来自计时模块的0~59s输出数据，用于各个状态的时间分配；forward,reverse,pause分别代表正转、反转、暂停三个状态的输出信号；djs(5:0)为预置时间倒计时的输出信号。模块程序如下：

```verilog 
module control(  
                   input wire clk,
                   input wire clk1hz,
                   input wire start,
                   input wire [5:0] count,
                   output reg forward,
                   output reg reverse,
                   output reg pause,
                   output reg [5:0] djs
                                               );
always @ (posedge clk)
   begin 
         forward <= 1;
         reverse <= 1;
         pause <= 1;
      if(djs == 0)
         begin
         forward <= 1;
         reverse <=1;
         pause <= 1;
         end
       else if(count < 20)
          forward <= 0;  
       else if(count < 30)
          pause <= 0;    
       else if(count < 50)
          reverse <= 0;
       else 
          pause <= 0;
   end
always @ (posedge clk1hz or negedge start)
   begin
     if(!start)
        djs <= 45;
     else if(djs != 0 && count == 59)
        djs <= djs - 1;
     else 
        djs <= djs;
    end                                                                     
endmodule
```

#### 4）时间显示器

时间显示器设计的主要任务是：对来自状态控制器分配的倒计时工作时间进行十位与个位的分位处理，并对分位后得到的两位0~9十进制数进行数码管七段码的译码处理。

由于预置洗衣机工作时间为45min，在进行个位与十位的分位处理时，在一个always快内用if语句对输入信号进行0~9，10~19,20~29,30~39，40~49时段的判别，产生个位与十位的十进制输出。再用另外的进程对产生的十进制数进行七段译码的编译。程序如下：

```verilog 
module display( 
                    input wire clk,
                    input wire start,
                    input wire [5:0] djs,
                    output reg [6:0] seg10,
                    output reg [6:0] seg0
                                               );
reg [4:0] cnt10;
reg [4:0] cnt0;
always @ (posedge clk or negedge start)
   begin
      if(!start)  begin
          cnt10 <= 4;
          cnt0 <= 5;
      end
      else if(djs >= 40) begin
          cnt10 <= 4;
          cnt0 <= djs-40;
      end
      else if(djs >= 30) begin
          cnt10 <= 3;
          cnt0 <= djs-30;
      end
      else if(djs >= 20) begin
          cnt10 <= 2;
          cnt0 <= djs-20;
      end
      else if(djs >= 10) begin
          cnt10 <= 1;
          cnt0 <= djs-10;
      end
      else begin
          cnt10 <= 0;
          cnt0 <= djs;
      end 
   end               
always @ (*)
   case(cnt10)
      0:seg10 = 7'b0000001;
      1:seg10 = 7'b1001111;
      2:seg10 = 7'b0010010;
      3:seg10 = 7'b0000110;
      4:seg10 = 7'b1001100;
      default : seg10 =  7'b1111111;
   endcase 
always @ (*)
      case(cnt0)
         0:seg0 = 7'b0000001;
         1:seg0 = 7'b1001111;
         2:seg0 = 7'b0010010;
         3:seg0 = 7'b0000110;
         4:seg0 = 7'b1001100;
         5:seg0 = 7'b0100100;
         6:seg0 = 7'b0100000;
         7:seg0 = 7'b0001111;
         8:seg0 = 7'b0000000;
         9:seg0 = 7'b0000100;
         default : seg0 =  7'b1111111;
      endcase                                                          
endmodule
```

#### 5）扫描显示驱动模块
扫描显示驱动模块的主要任务是：采用扫描方式对洗衣机倒计时的时间数据的个位与十位四组七段译码进行快速分时复用处理。

Clk为系统时钟；clk1k为1khz输入信号，用于快速扫描信号；start为开始信号，时间输出显示为60；seg0(6:0)、seg10(6:0)为时间输出显示的个位与十位七段码数据；dig为四位数码管的位选信号，seg为数码管段选信号，低电平有效，模块源程序如下：

```verilog 
module scan(
                input wire clk,
                input wire clk1k,
                input wire start,
                input wire [6:0] seg10,
                input wire [6:0] seg0,
                output wire [3:0] dig,
                output reg [6:0] seg
                                          );
reg [15:0] cnt;
reg [3:0] an;
initial 
  an = 4'b1110;
always @ (posedge clk1k)
     begin
       an[3:1] <= an[2:0];
       an[0] <= an[3];
      end
always @ (*)
   case(an)
     4'b1101:seg <= seg10;
     4'b1110:seg <= seg0;
     default:seg <= 7'b1111111;
    endcase  
  assign dig = an;                                            
endmodule
```

###  2、系统顶层设计

```verilog 
module top(  
               input wire clk,
               input wire start,
               output wire forward,
               output wire reverse,
               output wire pause,
               output wire [3:0] dig,
               output wire [6:0] seg
                                         );
wire clk1hz;
wire clk1k;
wire [5:0] count;
wire [5:0] djs;
wire [6:0] seg10;
wire [6:0] seg0;
clkdiv   U1(
               .clk(clk),
               .clk1hz(clk1hz),
               .clk1k(clk1k)
               );
counter  U2(
               .clk1hz(clk1hz),
               .start(start),
               .count(count)
               );
control  U3(
               .clk(clk),
               .clk1hz(clk1hz),
               .start(start),
               .count(count),
               .forward(forward),
               .reverse(reverse),
               .pause(pause),
               .djs(djs)
               );
display  U4(
               .clk(clk),
               .start(start),
               .djs(djs),
               .seg10(seg10),
               .seg0(seg0)
               );
scan     U5(
               .clk(clk),
               .clk1k(clk1k),
               .start(start),
               .seg10(seg10),
               .seg0(seg0),
               .dig(dig),
               .seg(seg)
               );
                             
endmodule
```

##  3、	引脚分配与实现

将设计好的洗衣机控制器顶层文件及下层模块进行编译、综合。引脚分配和编程，最后下载至FPGA开发板AX7020芯片内，利用开发板的按键输入和LED输出对系统进行 测试，观测并记录输出结果。洗衣机控制器引脚分配程序如下：

```verilog 
set_property IOSTANDARD LVCMOS33 [get_ports {dig[3]}]
set_property IOSTANDARD LVCMOS33 [get_ports {dig[2]}]
set_property IOSTANDARD LVCMOS33 [get_ports {dig[1]}]
set_property IOSTANDARD LVCMOS33 [get_ports {dig[0]}]
set_property IOSTANDARD LVCMOS33 [get_ports {seg[6]}]
set_property IOSTANDARD LVCMOS33 [get_ports {seg[5]}]
set_property IOSTANDARD LVCMOS33 [get_ports {seg[4]}]
set_property IOSTANDARD LVCMOS33 [get_ports {seg[3]}]
set_property IOSTANDARD LVCMOS33 [get_ports {seg[2]}]
set_property IOSTANDARD LVCMOS33 [get_ports {seg[1]}]
set_property IOSTANDARD LVCMOS33 [get_ports {seg[0]}]
set_property IOSTANDARD LVCMOS33 [get_ports clk]
set_property IOSTANDARD LVCMOS33 [get_ports forward]
set_property IOSTANDARD LVCMOS33 [get_ports pause]
set_property IOSTANDARD LVCMOS33 [get_ports reverse]
set_property IOSTANDARD LVCMOS33 [get_ports start]
set_property PACKAGE_PIN N17 [get_ports {dig[3]}]
set_property PACKAGE_PIN B19 [get_ports {dig[2]}]
set_property PACKAGE_PIN T11 [get_ports {dig[1]}]
set_property PACKAGE_PIN U13 [get_ports {dig[0]}]
set_property PACKAGE_PIN V12 [get_ports {seg[6]}]
set_property PACKAGE_PIN C20 [get_ports {seg[5]}]
set_property PACKAGE_PIN V15 [get_ports {seg[4]}]
set_property PACKAGE_PIN P14 [get_ports {seg[3]}]
set_property PACKAGE_PIN W18 [get_ports {seg[2]}]
set_property PACKAGE_PIN T12 [get_ports {seg[1]}]
set_property PACKAGE_PIN W14 [get_ports {seg[0]}]
set_property PACKAGE_PIN U18 [get_ports clk]
set_property PACKAGE_PIN M14 [get_ports forward]
set_property PACKAGE_PIN M15 [get_ports pause]
set_property PACKAGE_PIN K16 [get_ports reverse]
set_property PACKAGE_PIN T17 [get_ports start]
```
遇到的问题：数码管显示不正常 

补充：数码管显示秒
